startshape Scene
/*
Cool forest scene, needs some downgrading to work
http://www.contextfreeart.org/gallery/view.php?id=3407

CF::Impure=1
CF::Size=[s (192/9) (108/9)]
CF::ColorDepth=16
Fmin=2
Fmax=8
N=16
*/
shape Scene
{ SQUARE[y 13.35 s 30 b 0.98 sat 0.1 hue  55 z -100]
  Forest(0,0)[y -4]
}
shape Forest (natural F,R)
{ if (F<=Fmax)
  { Sgn=2*randint(2)-1
    Forest(F+1,0  ) [ s .7 y .7 x ( Sgn*3.0) b .05 z -1 a
-0.09]
    Forest(F+1,R+1) [ s .7 y .7 x (-Sgn*4.0) b .05 z -1 a
-0.05]
    if (F>=Fmin &&(F==R ||rand(1.3^F)<1)) Grow(F,-1,1,0) [s
0.5]
} }
shape Grow (number G,B,F,R) 
rule 0.5 { Grow(G,B,-F,R) [ flip 90 ]}
rule     { 
  if(G>=N) {if (G<N+2) Blossom[r (180-F*R) z 0.01]}
  else { L=rand(2,7) LH=L/2
    SQUARE [ y LH s 1 L sat 1 hue 10 b 0.05]
    SQUARE [ y LH s (abs(R/75)) L sat 0 b 1 x (R*F/400) z
0.1]
    CIRCLE [ y L  s 1.1 sat 1 hue 20 b 0.08]
    if(B<1) if(R*F<=0)  Grow(G+1,B+1,F,R+F*19) [   s .9 y L
r  19 ]
            else        Grow(G+1,B+1,F,R-F*13) [   s .9 y L
r -13 ]
    else { if(R*F< 150) Grow(G,0,F,R+F*35) [ r  35 s .9 y L
z -0.1]
           if(R*F>-120) Grow(G,0,F,R-F*30) [ r -30 s .7 y L
z  0.1]
} } }
shape Blossom
{ transform [r rand(-5,13) s 1.6]
  { SQUARE [[ y 10     s 1 20 sat 0.2 hue 70 b 1]]
    CIRCLE [[ y -1     s 6  1 sat 0.1 hue 90 b 0.90]]
    CIRCLE [[ y  2 x 3 s 1  3 sat 0.3 hue 90 b 0.75]]
    CIRCLE [[ y  2 x-3 s 1  3 sat 0.3 hue 90 b 0.70]]
    CIRCLE [[ y  8     s 2  4 sat 1   hue 20 b 0.80]]
    CIRCLE [[ y 16     s 2  6 sat 0.4 hue 10 b 0.90]]
} }
